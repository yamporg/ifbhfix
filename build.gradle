plugins {
    id 'net.minecraftforge.gradle' version '4.0.13'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

group 'io.github.yamporg.ifbhfix'
version '0.0.5'

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            properties(
                'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP',
                'forge.logging.console.level': 'debug',
                'mixin.hotSwap': 'true',
                'mixin.debug': 'true',
                'mixin.env.disableRefMap': 'true',
            )
        }
    }
}

repositories {
    maven { url 'https://cursemaven.com' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

    // https://www.curseforge.com/minecraft/mc-mods/shadowfacts-forgelin/files/2785465
    // Filename Forgelin-1.8.4.jar
    implementation 'curse.maven:forgelin-248453:2785465'

    // https://www.curseforge.com/minecraft/mc-mods/tesla-core-lib/files/2891841
    // Filename tesla-core-lib-1.12.2-1.0.17.jar
    implementation 'curse.maven:teslacorelib-254602:2891841'

    // https://www.curseforge.com/minecraft/mc-mods/industrial-foregoing/files/2745321
    // Filename industrialforegoing-1.12.2-1.12.13-237.jar
    implementation fg.deobf('curse.maven:industrialforegoing-266515:2745321')
}

// And here come the workarounds. A lot of them.

// Minecraft 1.12 requires both 2.9.4-nightly-20150209 and 2.9.2-nightly-20140822,
// and the latter does not exist. We fix that by using the newer version.
//
// See https://github.com/MinecraftForge/ForgeGradle/issues/627
configurations.all {
    resolutionStrategy {
        eachDependency {
            if (requested.group == 'org.lwjgl.lwjgl' && requested.name == 'lwjgl-platform') {
                useVersion '2.9.4-nightly-20150209'
            }
        }
    }
}

// Enable sourcesJar task and set source and target Java version.
java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// For some reason resources are not loaded in runClient task
// unless we put them to class dir directly. Newer version of
// Minecraft work just fine though. Thatâ€™s weird.
//
// See https://github.com/MinecraftForge/ForgeGradle/issues/717
// See also https://redd.it/e4hfzz
//
// The current workaround comes from Chiseled Me mod.
// See https://github.com/necauqua/chiseled-me/blob/f8b94ab08ff39c46c58120027de4361597a1a473/build.gradle#L30-L39
afterEvaluate {
    tasks.findByName('prepareRuns')?.doLast {
        copy {
            from 'build/resources/main'
            into 'build/classes/java/main'
        }
    }
}

// Make archive builds reproducible.
//
// See https://docs.gradle.org/6.8.1/userguide/working_with_files.html#sec:reproducible_archives
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

// Set zero timestamps in reobfJar task too.
//
// See https://github.com/MinecraftForge/ForgeGradle/issues/358
// See https://github.com/md-5/SpecialSource/issues/40
tasks.withType(net.minecraftforge.gradle.userdev.tasks.RenameJarInPlace) {
    def xs = ['--stable']
    args.each { xs << it }
    args = xs.toArray([] as String[])

    // And --stable option is available since v1.8.5 while ForgeGradle uses v1.8.3.
    // See https://github.com/md-5/SpecialSource/commit/5dbf0ee84ae02085b4bce7ea9101f16ed6ba8f29
    tool = 'net.md-5:SpecialSource:1.8.6:shaded' 
}

curseforge {
    if (project.hasProperty('curseForgeApiKey')) {
        apiKey = project.curseForgeApiKey
    }
    project {
        id = '446961'
        releaseType = 'release'

        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')

        mainArtifact(jar) {
            relations {
                requiredDependency 'industrial-foregoing'
            }
        }
        addArtifact sourcesJar
    }
}
